allprojects {
	version = "1.0"
}

apply plugin: "eclipse"

subprojects {
	apply plugin: "java"
	apply plugin: "eclipse"
	apply plugin: "deliveryResources"

	sourceCompatibility = 1.7
	targetCompatibility = 1.7

	compileJava.options.encoding = "UTF-8"
	compileTestJava.options.encoding = "UTF-8"

	sourceSets.main.resources.srcDir "src/main/java"
	sourceSets.test.resources.srcDir "src/test/java"

	repositories {
		mavenCentral()
		maven {
			url "http://repository.jboss.org/nexus/content/repositories/public/"
		}
	}

	configurations {
		all*.exclude group: "commons-logging", module: "commons-logging"
		provided
	}

	sourceSets.main.compileClasspath += [configurations.provided]
	eclipse.classpath.plusConfigurations += [configurations.provided]

	if (parent.hasProperty("appendix")) {
		project.appendix = parent.appendix
	}

	deliveryResources {
		from "src/main/distResources"
		tokens "../filter.properties"
		override { props ->
			def appdir = "/opt/" + parent.name
			if (project.hasProperty("appendix")) {
				appdir = appdir + "/" + project.appendix
			}
			for (String dir : ["admin", "entree"]) {
				props.setProperty("filter.${dir}.conf", "${appdir}/${dir}/conf")
				props.setProperty("filter.${dir}.log.dir", "${appdir}/${dir}/log")
			}
			for (String dir : ["batch"]) {
				props.setProperty("filter.${dir}.conf", "${appdir}/${dir}/conf")
				props.setProperty("filter.${dir}.log.dir", "${appdir}/${dir}/job/\${jobId}/log")
				props.setProperty("filter.${dir}.log.dir.common", "${appdir}/${dir}/log")
			}
		}
	}
}

def deliverySpec = copySpec {

	for (String dir : ["admin", "entree"]) {
		into("${dir}/app") {
			from("${dir}/build/libs")
			include "*.war"
			rename {
				if (project.hasProperty("appendix")) {
					it.replace("-${version}", project.appendix)
				} else {
					it.replace("-${version}", "")
				}
			}
		}
	}

	for (String dir : ["batch"]) {
		into("${dir}") {
			from("${dir}/build/install/${dir}")
		}
	}

	for (String dir : ["admin", "entree", "batch"]) {
		into("${dir}/conf") {
			from("${dir}/build/resources/main") {
				include "*.properties"
				exclude "${dir}.properties"
				exclude "log.properties"
				rename { it + ".template" }
			}
		}
		into("${dir}/log") {
		}
	}
}

task deliveryZip(type: Zip) {
	baseName = project.name
	version = project.version
	if (project.hasProperty("appendix")) {
		appendix = project.appendix
	}
	classifier = "delivery"
	with(deliverySpec)
	dependsOn ":admin:assembleDelivery"
	dependsOn ":entree:assembleDelivery"
	dependsOn ":batch:assembleDelivery"
	dependsOn ":batch:installApp"
}

task deliveryTar(type: Tar) {
	baseName = project.name
	version = project.version
	if (project.hasProperty("appendix")) {
		appendix = project.appendix
	}
	classifier = "delivery"
	compression = Compression.GZIP
	with(deliverySpec)
	dependsOn ":admin:assembleDelivery"
	dependsOn ":entree:assembleDelivery"
	dependsOn ":batch:assembleDelivery"
	dependsOn ":batch:installApp"
}
