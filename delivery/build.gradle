def distSpec = copySpec {

	for (String dir : ["admin", "site"]) {
		into("${dir}/app") {
			from("../${dir}/build/libs")
			include "*.war"
		}
	}

	for (String dir : ["batch"]) {
		into("${dir}") {
			from("../${dir}/build/install/${dir}")
			exclude "resources/**"
		}
	}

	for (String dir : ["admin", "site", "batch"]) {
		into("${dir}/conf") {
			from("../${dir}/build/resources/main") {
				include "*.properties"
				exclude "${dir}.properties"
				exclude "log.properties"
				rename { it + ".template" }
			}
		}
		into("${dir}/log") {
		}
	}
}

task distZip(type: Zip) {
	baseName = parent.name
	classifier "dist"
	with(distSpec)
	dependsOn ":admin:war", ":site:war", ":batch:installApp"
}

task distTar(type: Tar) {
	baseName = parent.name
	classifier "dist"
	compression = Compression.GZIP
	with(distSpec)
	dependsOn ":admin:war", ":site:war", ":batch:installApp"
}
