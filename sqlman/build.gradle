apply plugin: "war"
apply plugin: "eclipse-wtp"
apply plugin: "jetty"
apply plugin: "jettyEclipse"

dependencies {
	compile project(":goods")
	compile project(":foundation")
	compile "org.apache.tiles:tiles-core:${versionTiles}"
	compile "org.apache.tiles:tiles-api:${versionTiles}"
	compile "org.apache.tiles:tiles-servlet:${versionTiles}"
	compile "org.apache.tiles:tiles-jsp:${versionTiles}"
	compile "org.apache.tiles:tiles-el:${versionTiles}"
	compile "org.apache.tiles:tiles-extras:${versionTiles}"
	compile "javax.servlet:jstl:${versionJstl}"
	runtime "org.flywaydb:flyway-core:${versionFlyway}"
	runtime "ch.qos.logback:logback-core:${versionLogback}"
	runtime "ch.qos.logback:logback-classic:${versionLogback}"
	providedCompile "javax.servlet:javax.servlet-api:${versionJavaServlet}"
	providedCompile "javax.servlet.jsp:javax.servlet.jsp-api:${versionJavaJsp}"
	providedCompile "javax.jms:jms-api:${versionJavaJms}"
	providedCompile "javax.transaction:transaction-api:${versionJavaTransaction}"
	compileOnly "org.projectlombok:lombok:${versionLombok}"
	testRuntime "com.h2database:h2:${versionH2db}"
	testCompile "org.springframework:spring-test:${versionSpring}"
	testCompile "junit:junit:${versionJunit}"
	testCompile "org.hamcrest:hamcrest-all:${versionHamcrest}"
	testCompile "org.mockito:mockito-all:${versionMockito}"
	testRuntime "org.glassfish:javax.el:${versionJavaEl}"
}

eclipse.wtp.facet {
	facet name: "java", version: "1.7"
	facet name: "jst.web", version: "3.0"
}

war {
	classpath = jar.outputs.files + configurations.runtime - configurations.providedRuntime
}

buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("com.sahlbach.gradle:gradle-jetty-eclipse-plugin:1.9.+") { exclude group: 'org.eclipse.jetty' }
		classpath 'org.eclipse.jetty:jetty-webapp:9.2.12.v20150709'
		classpath 'org.eclipse.jetty:jetty-plus:9.2.12.v20150709'
		classpath 'org.eclipse.jetty:jetty-jsp:9.2.12.v20150709'
		classpath 'org.eclipse.jetty:jetty-annotations:9.2.12.v20150709'
		classpath "com.h2database:h2:${versionH2db}"
	}
}

[jettyRun, jettyRunWar]*.with {
	jettyConfig = file("jetty-config.xml")
	additionalRuntimeJars = buildscript.configurations.classpath
}

jettyEclipse {
	jettyConfig = file("jetty-eclipse-config.xml")
	additionalRuntimeJars = buildscript.configurations.classpath
}

def messageDir = "src/main/resources/message"
task mergeformmessage << {
	ant.concat(destfile: "${messageDir}/form/form.properties", encoding: "UTF-8", outputencoding: "UTF-8") {
		fileset(dir: "${messageDir}/form/") {
			include(name: "**.txt")
			exclude(name: "**.properties")
		}
	}
	ant.native2ascii(src: "${messageDir}/form/", dest: "${messageDir}/", includes: "form.properties", encoding: "UTF-8")
}
